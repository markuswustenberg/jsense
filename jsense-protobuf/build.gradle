version '0.2.0'
description 'A Java library to work with sensing. Because sensing is nice. This is a supplemental library to allow serialization and deserialization to/from Google Protocol Buffers format.'

apply plugin: 'protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'ws.antonov.gradle.plugins:gradle-plugin-protobuf:0.8'
    }
}

def excludePattern = '/org/jsense/serialize/gen/'
def excludePatternAntStyle = '**' + excludePattern + '*'
tasks.withType(FindBugs) {
    classes = classes.filter {
        !it.path.contains(excludePattern)
    }
}
tasks.withType(Checkstyle) {
    exclude excludePatternAntStyle
}
tasks.withType(Pmd) {
    exclude excludePatternAntStyle
}

dependencies {
    compile project(':jsense-core')

    compile 'com.google.guava:guava:17.0'
    compile 'com.google.protobuf:protobuf-java:2.5.0'
    compile 'joda-time:joda-time:2.3'

    testCompile 'junit:junit:4.+'
    testCompile project(':jsense-core').sourceSets.test.output
}
