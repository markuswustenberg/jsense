subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    group 'org.jsense'
    status 'release'

    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'

    repositories {
        mavenCentral()
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    checkstyle {
        configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
        showViolations = false
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.allJava
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from javadoc.destinationDir
    }

    publishing {
        publications {
            java(MavenPublication) {
                from components.java
                artifact sourceJar {
                    classifier "sources"
                }
                artifact javadocJar {
                    classifier "javadoc"
                }
                pom.withXml {
                    // The fourth child element is "version", this could probably be improved
                    asNode().children().get(3) + {
                        // The following line is so that the nested closure works
                        resolveStrategy = Closure.DELEGATE_FIRST
                        packaging 'jar'
                        name project.group + ':' + project.name
                        description 'A Java library to work with sensing. Because sensing is nice.'
                        url 'http://www.jsense.org'
                        licenses {
                            license {
                                name 'MIT License'
                                url 'http://opensource.org/licenses/MIT'
                                distribution 'repo'
                            }
                        }
                        scm {
                            url 'https://github.com/markuswustenberg/jsense'
                            connection 'scm:git:git://github.com/markuswustenberg/jsense.git'
                            developerConnection 'scm:git:ssh:git@github.com:markuswustenberg/jsense.git'

                        }
                        developers {
                            developer {
                                id 'markuswustenberg'
                                name 'Markus WÃ¼stenberg'
                                email 'markus@wustenberg.dk'
                            }
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name 'snapshot'
                url 'https://oss.sonatype.org/content/repositories/snapshots'
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
            maven {
                name 'release'
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                credentials {
                    username = sonatypeUsername
                    password = sonatypePassword
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
