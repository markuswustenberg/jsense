buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.0.0' // cobertura plugin
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:0.4.0'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.6.1'
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'nexus'
    apply plugin: 'findbugs'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    apply plugin: 'cobertura'
    apply plugin: 'coveralls'

    group 'org.jsense'

    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6

    repositories {
        mavenCentral()
    }

    javadoc {
        exclude '**/gen/**'
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    checkstyle {
        configFile = new File(rootDir, 'config/checkstyle/checkstyle.xml')
        showViolations = false
    }

    cobertura.coverageFormats = ['html', 'xml'] // coveralls plugin depends on xml format report

    modifyPom {
        project {
            name project.group + ':' + project.name
            description project.description
            url 'http://www.jsense.org'
            packaging 'jar'
            licenses {
                license {
                    name 'MIT License'
                    url 'http://opensource.org/licenses/MIT'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/markuswustenberg/jsense'
                connection 'scm:git:git://github.com/markuswustenberg/jsense.git'
                developerConnection 'scm:git:ssh:git@github.com:markuswustenberg/jsense.git'

            }
            developers {
                developer {
                    id 'markuswustenberg'
                    name 'Markus WÃ¼stenberg'
                    email 'markus@wustenberg.dk'
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.11'
}
